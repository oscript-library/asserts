Процедура Проверить(Условие, ДопСообщениеОшибки = "") Экспорт

	Если Условие Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Переданный параметр (%1) не является Истиной, а хотели, чтобы являлся. %2",
		Формат(Условие, "БЛ=ложь; БИ=истина"),
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

Процедура ПроверитьИстину(Условие, ДопСообщениеОшибки = "") Экспорт
	Проверить(Условие, ДопСообщениеОшибки);
КонецПроцедуры

Процедура ПроверитьЛожь(Условие, ДопСообщениеОшибки = "") Экспорт

	Если Не Условие Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Переданный параметр (%1) не является Ложью, а хотели, чтобы являлся. %2",
		Формат(Условие, "БЛ=ложь; БИ=истина"),
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

Процедура ПроверитьДату(Дата, Период, ДопСообщениеОшибки = "") Экспорт

	Если Дата > Период.ДатаНачала И Дата < Период.ДатаОкончания Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Переданный параметр (%1) не входит в период %2, а хотели, чтобы входил. %3",
		Формат(Дата, "ДФ='dd.MM.yyyy HH:mm:ss'"),
		ПредставлениеПериода(Период.ДатаНачала, Период.ДатаОкончания, "ФП = Истина")
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

Процедура ПроверитьРавенствоДатСТочностью2Секунды(Дата, Дата2, ДопСообщениеОшибки = "") Экспорт

	Если Дата > Дата2 - 2 И Дата < Дата2 + 2 Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Переданная дата (%1) не равна дате (%2) с точностью до 2-х секунд, а хотели, чтобы они равнялись. %3",
		Формат(Дата, "ДФ='dd.MM.yyyy HH:mm:ss'"),
		Формат(Дата2, "ДФ='dd.MM.yyyy HH:mm:ss'"),
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

Процедура ПроверитьРавенство(ПервоеЗначение, ВтороеЗначение, ДопСообщениеОшибки = "") Экспорт
	Если ПервоеЗначение <> ВтороеЗначение Тогда
		СообщениеОшибки = "Сравниваемые значения ("+ПервоеЗначение+"; "+ВтороеЗначение+") не равны, а хотели, чтобы были равны." + ФорматДСО(ДопСообщениеОшибки);
		ВызватьОшибкуУтверждения(СообщениеОшибки);
	КонецЕсли;
КонецПроцедуры

Процедура ПроверитьНеРавенство(ПервоеЗначение, ВтороеЗначение, ДопСообщениеОшибки = "") Экспорт

	Если ПервоеЗначение <> ВтороеЗначение Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Сравниваемые значения (%1; %2) равны, а хотели, чтобы были не равны. %3",
		ПервоеЗначение,
		ВтороеЗначение,
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

Процедура ПроверитьБольше(Больше, Меньше, ДопСообщениеОшибки = "") Экспорт

	Если Больше > Меньше Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Первый параметр (%1) меньше или равен второму (%2) а хотели, чтобы был больше. %3",
		Больше,
		Меньше,
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

Процедура ПроверитьБольшеИлиРавно(Больше, Меньше, ДопСообщениеОшибки = "") Экспорт

	Если Больше >= Меньше Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Первый параметр (%1) меньше второго (%2) а хотели, чтобы был больше или равен. %3",
		Больше,
		Меньше,
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

Процедура ПроверитьМеньше(Меньше, Больше, ДопСообщениеОшибки = "") Экспорт

	Если Больше > Меньше Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Первый параметр (%1) больше или равен второму (%2) а хотели, чтобы был меньше. %3",
		Меньше,
		Больше,
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

Процедура ПроверитьМеньшеИлиРавно(Меньше, Больше, ДопСообщениеОшибки = "") Экспорт

	Если Больше >= Меньше Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Первый параметр (%1) больше второго (%2) а хотели, чтобы был больше или равен. %3",
		Меньше,
		Больше,
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

// проверка идет через ЗначениеЗаполнено, но мутабельные значение всегда считаем заполненными
Процедура ПроверитьЗаполненность(ПроверяемоеЗначение, ДопСообщениеОшибки = "") Экспорт

	Попытка
        Заполнено = ЗначениеЗаполнено(ПроверяемоеЗначение);
    Исключение
        Заполнено = Истина;
    КонецПопытки;

	Если Заполнено Тогда
		Возврат;
	КонецЕсли;

	ВызватьОшибкуУтверждения(
		СтрШаблон(
			"Значение (%1) не заполнено, а ожидалась заполненность %2",
			ПроверяемоеЗначение,
			ФорматДСО(ДопСообщениеОшибки)
		)
	);

КонецПроцедуры

Процедура ПроверитьНеЗаполненность(ПроверяемоеЗначение, ДопСообщениеОшибки = "") Экспорт

    СообщениеОшибки = СтрШаблон(
		"Значение (%1) заполнено, а ожидалась незаполненность",
		ПроверяемоеЗначение,
		ФорматДСО(ДопСообщениеОшибки)
	);

	Попытка
        Заполнено = ЗначениеЗаполнено(ПроверяемоеЗначение);
    Исключение
        Заполнено = Истина;
    КонецПопытки;

    Если Заполнено Тогда
        ВызватьОшибкуУтверждения(СообщениеОшибки);
    КонецЕсли;

КонецПроцедуры

Процедура ПроверитьТип(Значение, ТипИлиИмяТипа, ДопСообщениеОшибки = "") Экспорт

	Если ТипЗнч(ТипИлиИмяТипа) = Тип("Строка") Тогда
		ОжидаемыйТип = Тип(ТипИлиИмяТипа);
	ИначеЕсли ТипЗнч(ТипИлиИмяТипа) = Тип("Тип") Тогда
		ОжидаемыйТип = ТипИлиИмяТипа;
	Иначе

		ТекстИсключения = СтрШаблон(
			"ПроверитьТип: Тип значения параметра ТипИлиИмяТипа должен быть <Тип> или <Строка>, а получили <%1>. %2",
			ТипЗнч(ТипИлиИмяТипа),
			ФорматДСО(ДопСообщениеОшибки)
		);

		ВызватьОшибкуУтверждения(ТекстИсключения);

	КонецЕсли;

	Если ТипЗнч(Значение) = ОжидаемыйТип Тогда
		Возврат;
	КонецЕсли;

	ОшибкаПроверки = СтрШаблон(
		"Типом значения <%1> является <%2>, а ожидался тип <%3>. %4",
		Значение,
		ТипЗнч(Значение),
		ТипИлиИмяТипа,
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(ОшибкаПроверки);

КонецПроцедуры

Процедура ПроверитьЧтоВСтрокеСодержатсяТолькоЦифры(Знач ИсходнаяСтрока, ДопСообщениеОшибки = "") Экспорт

	// Порт из testrunner в составе oscript-engine

	СообщениеОшибки = СтрШаблон(
		"Строка (%1) содержит не только цифры, а ожидалась обратное %2",
		ИсходнаяСтрока,
		ФорматДСО(ДопСообщениеОшибки)
	);

	ТолькоЦифры = Ложь;
	ДлинаСтроки = СтрДлина(ИсходнаяСтрока);

	Для Сч = 1 По ДлинаСтроки Цикл

		ТекущийСимвол = КодСимвола(Сред(ИсходнаяСтрока, Сч, 1));

		Если 48 <= ТекущийСимвол И ТекущийСимвол <= 57 Тогда
			ТолькоЦифры = Истина;
		Иначе
			ТолькоЦифры = Ложь;
			Прервать;
		КонецЕсли;

	КонецЦикла;

	Если НЕ ТолькоЦифры Тогда
		ВызватьОшибкуУтверждения(СообщениеОшибки);
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьВхождение(Строка, ПодстрокаПоиска, ДопСообщениеОшибки = "") Экспорт

	Если СтрНайти(Строка, ПодстрокаПоиска) <> 0 Тогда
		Возврат;
	КонецЕсли;

	СообщениеОшибки = СтрШаблон(
		"Искали в <%1> подстроку <%2>, но не нашли. %3",
		Строка,
		ПодстрокаПоиска,
		ФорматДСО(ДопСообщениеОшибки)
	);

	ВызватьОшибкуУтверждения(СообщениеОшибки);

КонецПроцедуры

Процедура ПроверитьКодСОшибкой( Код, Ошибка, ДопСообщениеОшибки = "" ) Экспорт

	ИнформацияОбОшибке = Неопределено;

	Попытка
		Сценарий = ЗагрузитьСценарийИзСтроки(Код);
		СообщениеОшибки = СтрШаблон("Ожидали ошибку '%1', но ее не было", Ошибка);
	Исключение

		ИнформацияОбОшибке = ИнформацияОбОшибке();
		ОписаниеОшибки     = ИнформацияОбОшибке.Описание;

		Если СтрНайти(ОписаниеОшибки, Ошибка ) <> 0 Тогда
			Возврат;
		КонецЕсли;

		СообщениеОшибки = СтрШаблон("Ожидали ошибку '%1', а была ошибка %2", Ошибка, ОписаниеОшибки);

	КонецПопытки;

	ВызватьОшибкуУтверждения(СтрШаблон("%1 %2", СообщениеОшибки, ФорматДСО(ДопСообщениеОшибки)), ИнформацияОбОшибке);

КонецПроцедуры

// Проверяет существование метода у объекта
// 
// Выбрасывает исключение, если метод отсутствует
//
// Параметры:
//  Объект  - Любой - Объект, у которого требуется проверить наличие метода
//  
//  ИмяМетода  - Строка - Имя метода, наличие которого требуется проверить
//  
//  ДопСообщениеОшибки  - Строка - Необязательный. Дополнительное пояснение проверки. 
//                 Включается в текст исключения в случае, если проверка не была пройдена
Процедура ПроверитьМетодСуществует(Знач Объект, Знач ИмяМетода, Знач ДопСообщениеОшибки = "") Экспорт

	Если Новый Рефлектор.МетодСуществует(Объект, ИмяМетода) Тогда
		Возврат
	КонецЕсли;

	ВызватьОшибкуУтверждения(
		СтрШаблон(
			"Ожидали, что объект имеет метод с именем %1. %2",
			ИмяМетода,
			ФорматДСО(ДопСообщениеОшибки)
		)
	);

КонецПроцедуры

Функция ФорматДСО(ДопСообщениеОшибки)

	Если ДопСообщениеОшибки = "" Тогда
		Возврат "";
	КонецЕсли;

	Возврат Символы.ПС + ДопСообщениеОшибки;

КонецФункции

Функция ПредставлениеПериода(ДатаНачала, ДатаОкончания, ФорматнаяСтрока = Неопределено)
	Возврат СтрШаблон("с %1 по %2", ДатаНачала, ДатаОкончания);
КонецФункции

Процедура ВызватьОшибкуУтверждения(ТекстИсключения, Причина = Неопределено)

	Контекст = Новый ИсключениеОшибкаУтверждения();

	Если Число(Лев(Новый СистемнаяИнформация().Версия, 1)) >= 2 Тогда
		ВызватьИсключение Новый ИнформацияОбОшибке(ТекстИсключения, Контекст, Причина);
	Иначе
		ВызватьИсключение Новый ИнформацияОбОшибке(ТекстИсключения, Контекст);
	КонецЕсли;

КонецПроцедуры
