#Использовать ".."

Функция ПолучитьСписокТестов(юТест) Экспорт
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_Проверить_РавнаТаблице");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_РавнаТаблице");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ИмеетРавнуюСтруктуруСТаблицей");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ИмеетРавнуюСтруктуруСТаблицей_БезУчетаПорядкаКолонок");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_ИмеетРавнуюСтруктуруСТаблицей");
	ВсеТесты.Добавить("ТестДолжен_Проверить_НеравенствоТаблицСИзмененнымПорядкомСтрок");
	ВсеТесты.Добавить("ТестДолжен_Проверить_НеравенствоТаблицСИзмененнымПорядкомКолонок");
	ВсеТесты.Добавить("ТестДолжен_Проверить_СодержитСтрокуТаблицы");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_СодержитСтрокуТаблицы");
	ВсеТесты.Добавить("ТестДолжен_Проверить_РавнаТаблице_БезУчетаПорядкаСтрок");
	ВсеТесты.Добавить("ТестДолжен_Проверить_РавнаТаблице_БезУчетаПорядкаКолонок");
	ВсеТесты.Добавить("ТестДолжен_Проверить_РавнаТаблице_БезУчетаИменКолонок");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ТестДолжен_Проверить_РавнаТаблице() Экспорт
	Ожидаем.Что(Таблица1()).РавнаТаблице(Таблица1());
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_РавнаТаблице() Экспорт
	Ожидаем.Что(Таблица1()).Не_().РавнаТаблице(Таблица2());
КонецПроцедуры

Процедура ТестДолжен_Проверить_ИмеетРавнуюСтруктуруСТаблицей() Экспорт
	Таблица2 = Таблица1();
	Таблица2.Удалить(0);
	Ожидаем.Что(Таблица1()).ИмеетРавнуюСтруктуруСТаблицей(Таблица2);
КонецПроцедуры

Процедура ТестДолжен_Проверить_ИмеетРавнуюСтруктуруСТаблицей_БезУчетаПорядкаКолонок() Экспорт
	Ожидаем.Что(Таблица1()).БезУчетаПорядКаКолонок().ИмеетРавнуюСтруктуруСТаблицей(Таблица3());
КонецПроцедуры


Процедура ТестДолжен_Проверить_Отрицание_ИмеетРавнуюСтруктуруСТаблицей() Экспорт
	Ожидаем.Что(Таблица1()).Не_().ИмеетРавнуюСтруктуруСТаблицей(Таблица3());
КонецПроцедуры

Процедура ТестДолжен_Проверить_НеравенствоТаблицСИзмененнымПорядкомСтрок() Экспорт
	Таблица2 = Таблица1();
	Таблица2.Сортировать("К1 УБЫВ");
	Ожидаем.Что(Таблица1()).Не_().РавнаТаблице(Таблица2);
КонецПроцедуры

Процедура ТестДолжен_Проверить_НеравенствоТаблицСИзмененнымПорядкомКолонок() Экспорт
	Ожидаем.Что(Таблица1()).Не_().РавнаТаблице(Таблица3());
КонецПроцедуры

Процедура ТестДолжен_Проверить_НеравенствоТаблицСРазнымСоставомКолонок() Экспорт
	Таблица2 = Таблица1();
	Таблица2.Колонки.К1.Имя = "КК";
	Ожидаем.Что(Таблица1()).Не_().РавнаТаблице(Таблица2);
КонецПроцедуры

Процедура ТестДолжен_Проверить_СодержитСтрокуТаблицы() Экспорт
	СтрокаТаблицы = Таблица1()[0];
	Ожидаем.Что(Таблица1()).СодержитСтрокуТаблицы(СтрокаТаблицы);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_СодержитСТрокуТаблицы() Экспорт
	СтрокаТаблицы = Таблица1()[0];
	СтрокаТаблицы.К0 = "777";
	Ожидаем.Что(Таблица1()).Не_().СодержитСтрокуТаблицы(СтрокаТаблицы);
КонецПроцедуры

Процедура ТестДолжен_Проверить_РавнаТаблице_БезУчетаПорядкаСтрок() Экспорт
	Таблица2 = Таблица1();
	Таблица2.Сортировать("К1 УБЫВ");
	Ожидаем.Что(Таблица1()).БезУчетаПорядкаСтрок().РавнаТаблице(Таблица2);
КонецПроцедуры

Процедура ТестДолжен_Проверить_РавнаТаблице_БезУчетаПорядкаКолонок() Экспорт
	Ожидаем.Что(Таблица1()).БезУчетаПорядкаКолонок().РавнаТаблице(Таблица3());
КонецПроцедуры

Процедура ТестДолжен_Проверить_РавнаТаблице_БезУчетаИменКолонок() Экспорт
	Ожидаем.Что(Таблица1()).БезУчетаИменКолонок().РавнаТаблице(Таблица2());
КонецПроцедуры

// helpers
Функция Таблица1()

	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("К0");
	ТЗ.Колонки.Добавить("К1");
	ТЗ.Колонки.Добавить("К2");

	СтрТЗ = ТЗ.Добавить();
	СтрТЗ.К0 = "11";
	СтрТЗ.К1 = "12";
	СтрТЗ.К2 = "13";

	СтрТЗ = ТЗ.Добавить();
	СтрТЗ.К0 = "21";
	СтрТЗ.К1 = "22";
	СтрТЗ.К2 = "23";

	СтрТЗ = ТЗ.Добавить();
	СтрТЗ.К0 = "31";
	СтрТЗ.К1 = "32";
	СтрТЗ.К2 = "33";

	Возврат ТЗ;
КонецФункции

Функция Таблица2()

	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("А0");
	ТЗ.Колонки.Добавить("А1");
	ТЗ.Колонки.Добавить("А2");

	СтрТЗ = ТЗ.Добавить();
	СтрТЗ.А0 = "11";
	СтрТЗ.А1 = "12";
	СтрТЗ.А2 = "13";

	СтрТЗ = ТЗ.Добавить();
	СтрТЗ.А0 = "21";
	СтрТЗ.А1 = "22";
	СтрТЗ.А2 = "23";

	СтрТЗ = ТЗ.Добавить();
	СтрТЗ.А0 = "31";
	СтрТЗ.А1 = "32";
	СтрТЗ.А2 = "33";

	Возврат ТЗ;
КонецФункции

Функция Таблица3()

	ТЗ = Новый ТаблицаЗначений;
	ТЗ.Колонки.Добавить("К0");
	ТЗ.Колонки.Добавить("К2");
	ТЗ.Колонки.Добавить("К1");

	СтрТЗ = ТЗ.Добавить();
	СтрТЗ.К0 = "11";
	СтрТЗ.К1 = "12";
	СтрТЗ.К2 = "13";

	СтрТЗ = ТЗ.Добавить();
	СтрТЗ.К0 = "21";
	СтрТЗ.К1 = "22";
	СтрТЗ.К2 = "23";

	СтрТЗ = ТЗ.Добавить();
	СтрТЗ.К0 = "31";
	СтрТЗ.К1 = "32";
	СтрТЗ.К2 = "33";

	Возврат ТЗ;
КонецФункции
 
Процедура Инициализация()
	ОбъектТеста = ЗагрузитьСценарий(ОбъединитьПути(ТекущийСценарий().Каталог, "..", "src", "bdd-asserts.os"));
КонецПроцедуры

Инициализация();
