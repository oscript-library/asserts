&Тест
Процедура ТестДолжен_Проверить_ИмеетМетод_НеВыбрасываетИсключениеДляСуществующегоМетода() Экспорт
	Ожидаем
		.Что(Новый Массив)
		.ИмеетМетод("Добавить")
		;
КонецПроцедуры

&Тест
Процедура ТестДолжен_Проверить_ИмеетМетод_ВыбрасываетИсключениеДляОтсутствующегоМетода() Экспорт
	Попытка
		Ожидаем
			.Что(Новый Массив)
			.ИмеетМетод("ТакогоИмениМетодаНеДолжноБыть");
	Исключение
		Возврат;
	КонецПопытки;

	// тест должен завершиться в обработке исключения
	// если дошло сюда - это ошибка
	ВызватьИсключение "Проверка существования у объекта несуществующего метода должна была выбросить исключение, но этого не произошло";
КонецПроцедуры

&Тест
Процедура ТестДолжен_Проверить_ИмеетМетод_ВыбрасываетИсключениеСУказаннымТекстом() Экспорт
	Перем ЧастьТекста, Сообщение;

	ЧастьТекста = "ЭтотТекстДолженБытьВТекстеИсключения*#1~";
	Попытка
		Ожидаем
			.Что(Новый Массив, ЧастьТекста)
			.ИмеетМетод("ТакогоИмениМетодаНеДолжноБыть");
	Исключение
		Если 0 = СтрНайти(ОписаниеОшибки(), ЧастьТекста) Тогда
			Сообщение = "Ожидали, что проверка наличия несуществующего метода у объекта выбросит исключение с указанным текстом, но текст не был найден в тексте исключения" + Символы.ПС
						+ "Ожидаемый текст: " + ЧастьТекста + Символы.ПС
						+ "Текст исключения: " + ОписаниеОшибки();
			ВызватьИсключение Сообщение;
		Иначе
			Возврат;
		КонецЕсли;
	КонецПопытки;

	// тест должен завершиться в обработке исключения
	// если дошло сюда - это ошибка
	ВызватьИсключение "Проверка существования у объекта несуществующего метода должна была выбросить исключение, но этого не произошло";
КонецПроцедуры

&Тест
Процедура ТестДолжен_Проверить_ИмеетМетод_ПоддерживаетОтрицание() Экспорт
	Попытка
		Ожидаем
			.Что(Новый Массив)
			.Не_()
			.ИмеетМетод("ТакогоИмениМетодаНеДолжноБыть");
	Исключение
		ВызватьИсключение "Ожидали, что проверка существования метода совместно с отрицанием не выбросит исключение. " + ОписаниеОшибки();
	КонецПопытки;
КонецПроцедуры

&Тест
Процедура ТестДолжен_Проверить_ИмеетМетод_ПоддерживаетЦепныеВызовы() Экспорт
	Если ТипЗнч(Ожидаем.Что(Новый Массив).ИмеетМетод("Добавить")) <> Тип("БДДАссерт") Тогда
		ВызватьИсключение "Ожидали, что метод ИмеетМетод() поддерживает цепные вызовы (возвращает объект)";
	КонецЕсли;
КонецПроцедуры